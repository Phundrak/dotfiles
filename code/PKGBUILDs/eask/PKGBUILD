# Maintainer: Lucien Cartier-Tilet <lucien@phundrak.com>

_pkgname=eask
_orgname=emacs-eask
_nodepkgname=cli
pkgname="nodejs-${_pkgname}"
pkgver=0.9.3
pkgrel=1
pkgdesc="Command-line tool for building and testing Emacs Lisp packages"
url="https://github.com/${_orgname}/${_pkgname}"
license=("GPL3")
arch=("any")
depends=("nodejs")
makedepends=("npm")
source=("https://registry.npmjs.org/@${_orgname}/${_nodepkgname}/-/${_nodepkgname}-${pkgver}.tgz")
sha256sums=('1f3d948d89544c7680f882c9dee72f121a500947a42aa14b785ac949f729a3d0')
noextract=("$_pkgname-$pkgver.tgz")

prepare() {
  tar xf "$_nodepkgname-$pkgver.tgz" package/COPYING
}

package() {
  npm i --location=global --cache "${srcdir}/npm-cache" --prefix "$pkgdir/usr" "$srcdir/$_nodepkgname-$pkgver.tgz"
  install -Dm644 package/COPYING "$pkgdir/usr/share/licenses/${_pkgname}/COPYING"
  install -Dm644 package/README.md "$pkgdir/usr/share/doc/${_pkgname}/README.md"
}
